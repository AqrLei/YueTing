apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.aqrlei.graduation.yueting"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "1.0.1"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "x86_64","arm64-v8a","mips"
        }
    }
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false

    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            aidl.srcDirs += 'src/main/aidl'
            res.srcDirs = ['src/main/res/yueting/read',
                           'src/main/res/yueting/music',
                           'src/main/res/yueting/common',
                           'src/main/res/yueting/home',
                           'src/main/res/yueting',
                           'src/main/res'
            ]

        }
    }

    android.applicationVariants.all { variant ->
        variant.packageApplication.outputDirectory = new File("./apk/${variant.buildType.name}")
        variant.outputs.all {
            def fileName = "YueTing_${variant.buildType.name}_${defaultConfig.versionName}_${buildTime()}.apk"
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../YueTing.jks")
            storePassword "YueTing"
            keyAlias "yueting"
            keyPassword "YueTing"
        }
        release {
            storeFile file("../YueTing.jks")
            storePassword "YueTing"
            keyAlias "yueting"
            keyPassword "YueTing"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
    testImplementation 'junit:junit:4.12'
}
